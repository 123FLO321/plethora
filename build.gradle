// For those who want the bleeding edge
buildscript {
	repositories {
		jcenter()
		maven {
			name = "forge"
			url = "http://files.minecraftforge.net/maven"
		}
	}
	dependencies {
		classpath 'net.minecraftforge.gradle:ForgeGradle:2.2-SNAPSHOT'
	}
}

plugins {
	id 'com.matthewprenger.cursegradle' version '1.0.5'
}

buildDir = "build-${mc_version}"

apply plugin: 'net.minecraftforge.gradle.forge'
apply from: 'gradle/scripts/jsonGen.gradle'

version = mc_version + "-" + mod_version
group = "org.squiddev"
archivesBaseName = "plethora"

minecraft {
	version = "${project.mc_version}-${project.forge_version}"
	runDir = "run-${project.mc_version}"
	mappings = "snapshot_20161220"

	replace '${mod_version}', project.mod_version
	replace '${mc_version}', project.mc_version
	replace '${cc_version}', project.cc_version
}

jar {
	manifest {
		attributes(
			'FMLAT': 'Plethora_at.cfg',
		)
	}

	exclude "**/*.java"
}

repositories {
	jcenter()

	// JEI + Tinker's
	maven { url = "http://dvs1.progwml6.com/files/maven" }

	// ComputerCraft
	maven { url "https://cc.crzd.me/maven/" }

	// Chameleon + Storage Drawers
	ivy { artifactPattern "https://addons-origin.cursecdn.com/files/2358/893/[module]-[revision](.[ext])" }
	ivy { artifactPattern "https://addons-origin.cursecdn.com/files/2367/598/[module]-[revision](.[ext])" }

	// CCTweaks + ConfigGen
	maven { url = "https://dl.bintray.com/squiddev/maven" }

	// IC2 API
	maven { url = "http://maven.ic2.player.to/" }

	// Buildcraft
	ivy {
		artifactPattern "http://www.mod-buildcraft.com/releases/BuildCraft/[revision]/[module]-[revision](-[classifier]).[ext]"
	}

	// Multipart
	maven { url "http://maven.amadornes.com/" }

	// Refined Storage
	maven { url "http://dl.bintray.com/raoulvdberge/dev" }

	// Tesla
	maven { url 'http://maven.epoxide.xyz' }

	// RF
	maven { url = "http://maven.covers1624.net" }

	// Forestry
	ivy { artifactPattern "https://addons-origin.cursecdn.com/files/2379/506/[module]_[revision](.[ext])" }

	// Applied Energistics 2
	ivy { artifactPattern "https://addons-origin.cursecdn.com/files/2356/184/[module]-[revision](.[ext])" }

	// Baubles
	ivy { artifactPattern "https://addons-origin.cursecdn.com/files/2424/450/[module]-[revision](.[ext])"}

	// CBMP
	maven { url "http://chickenbones.net/maven" }
}

dependencies {
	compile "dan200.computercraft:ComputerCraft-SquidDev-CC-ComputerCraft-feature-minecraft-${project.mc_version}:${project.cc_version}-build10"
	deobfCompile "mezz.jei:jei_${project.mc_version}:4.2.5.233"

	provided "org.squiddev:CCTweaks:${project.mc_version}-1.5.0-pr0:dev"
	provided "net.industrial-craft:industrialcraft-2:2.6.99-ex110:dev"
	deobfProvided "buildcraft:buildcraft:7.99.6"
	provided "net.darkhax.tesla:Tesla:1.11-1.3.0.51:deobf"
	provided "cofh:CoFHCore:1.11.2-4.2.6.13:deobf"

	// Tinker's + Dependencies
	deobfProvided "slimeknights:TConstruct:${project.mc_version}-2.7.0.26"
	deobfProvided "slimeknights.mantle:Mantle:${project.mc_version}-1.2.0.26"

	// Storage drawers + Dependencies
	deobfProvided "curseMods:Chameleon:1.11-3.0.2"
	deobfProvided "curseMods:StorageDrawers:${project.mc_version}-4.1.0"

	provided "MCMultiPart2:MCMultiPart:2.0.2:deobf"
	deobfProvided "refinedstorage:refinedstorage:1.4.21-1251"
	deobfProvided "curseMods:forestry:${project.mc_version}-5.3.1.34"
	deobfProvided "curseMods:appliedenergistics2:rv4-alpha-9"
	deobfProvided "curseMods:Baubles:1.11-1.4.5"
	provided("codechicken:ForgeMultipart:${project.mc_version}-2.1.0.34:deobf") { exclude group: "codechicken" }
	provided "codechicken:CodeChickenLib:${project.mc_version}-2.6.0.264:deobf"

	provided('org.squiddev:ConfigGen:1.2.5') { exclude group: 'net.minecraftforge' }
}

processResources {
	// this will ensure that this task is redone when the versions change.
	inputs.property "mod_version", project.mod_version
	inputs.property "mc_version", project.mc_version

	// replace stuff in mcmod.info, nothing else
	from(sourceSets.main.resources.srcDirs) {
		include 'mcmod.info'

		// replace version and mcversion
		expand 'mod_version': mod_version, 'mc_version': mc_version
	}

	// copy everything else, thats not the mcmod.info
	from(sourceSets.main.resources.srcDirs) {
		exclude 'mcmod.info'
	}
}

gradle.projectsEvaluated {
	tasks.withType(JavaCompile) {
		options.compilerArgs << "-Xlint" << "-Xlint:-rawtypes" << "-Xlint:-processing"
	}
}

curseforge {
	apiKey = project.hasProperty('curseForgeApiKey') ? project.curseForgeApiKey : ''

	project {
		id = '248425'
		releaseType = 'release'
		changelog = ''

		relations {
			requiredLibrary 'computercraft'
		}
	}
}
