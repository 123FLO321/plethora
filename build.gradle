// For those who want the bleeding edge
buildscript {
	repositories {
		jcenter()
		maven {
			name = "forge"
			url = "http://files.minecraftforge.net/maven"
		}
	}
	dependencies {
		classpath 'net.minecraftforge.gradle:ForgeGradle:2.2-SNAPSHOT'
	}
}

plugins {
	id 'com.matthewprenger.cursegradle' version '1.0.5'
}

buildDir = "build-${mc_version}"

apply plugin: 'net.minecraftforge.gradle.forge'
apply from: 'gradle/scripts/jsonGen.gradle'

version = mc_version + "-" + mod_version
group = "org.squiddev"
archivesBaseName = "plethora"

minecraft {
	version = "${project.mc_version}-${project.forge_version}"
	runDir = "run-${project.mc_version}"
	mappings = "snapshot_20161220"

	replace '${mod_version}', project.mod_version
	replace '${mc_version}', project.mc_version
	replace '${cc_version}', project.cc_version
}

jar {
	manifest {
		attributes(
			'FMLAT': 'Plethora_at.cfg',
		)
	}

	exclude "**/*.java"
}

repositories {
	jcenter()

	// JEI + Tinker's
	maven { url = "http://dvs1.progwml6.com/files/maven" }

	// Baubles
	maven { url = "http://mvn.rx14.co.uk/local/" }

	// ComputerCraft
	maven { url "https://crzd.me/files/maven/" }

	// Chameleon + Storage Drawers
	ivy { artifactPattern "https://addons-origin.cursecdn.com/files/2358/893/[module]-[revision](.[ext])" }
	ivy { artifactPattern "https://addons-origin.cursecdn.com/files/2367/598/[module]-[revision](.[ext])" }

	// CCTweaks + ConfigGen
	maven { url = "https://dl.bintray.com/squiddev/maven" }

	// IC2 API
	maven { url = "http://maven.ic2.player.to/" }

	// Buildcraft
	ivy {
		artifactPattern "http://www.mod-buildcraft.com/releases/BuildCraft/[revision]/[module]-[revision](-[classifier]).[ext]"
	}

	// Multipart
	maven { url "http://maven.amadornes.com/" }

	// Refined Storage
	maven { url "http://dl.bintray.com/raoulvdberge/dev" }

	// Tesla
	maven { url 'http://maven.epoxide.xyz' }

	// Forestry
	ivy { artifactPattern "https://addons-origin.cursecdn.com/files/2379/506/[module]_[revision](.[ext])" }

	// Applied Energistics 2
	ivy { artifactPattern "https://addons-origin.cursecdn.com/files/2356/184/[module]-[revision](.[ext])" }
}

dependencies {
	compile "dan200.computercraft:ComputerCraft-SquidDev-CC-ComputerCraft-feature-minecraft-1.11.2:${project.cc_version}-build0"
	deobfCompile "mezz.jei:jei_${project.mc_version}:4.2.5.233"

	provided "org.squiddev:CCTweaks:${project.mc_version}-1.4.1:dev"
	provided "net.industrial-craft:industrialcraft-2:2.6.99-ex110:dev"
	provided "buildcraft:buildcraft:7.2.7:dev" // Won't run on 1.11.2 but just here for the future
	provided "com.azanor.baubles:Baubles:1.11-1.4.3.57:deobf"
	provided "net.darkhax.tesla:Tesla:1.11-1.3.0.51:deobf"

	// Tinker's + Dependencies
	deobfProvided "slimeknights:TConstruct:1.10.2-2.6.1.464" // Won't run on 1.11 yet
	deobfProvided "slimeknights.mantle:Mantle:1.10.2-1.1.3.199" // Won't run on 1.11 yet

	// Storage drawers + Dependencies
	deobfProvided "curseMods:Chameleon:1.11-3.0.2"
	deobfProvided "curseMods:StorageDrawers:${project.mc_version}-4.1.0"

	deobfProvided "MCMultiPart:MCMultiPart:1.3.0:universal"
	deobfProvided "refinedstorage:refinedstorage:1.4-953"
	deobfProvided "curseMods:forestry:${project.mc_version}-5.3.1.34"
	deobfProvided "curseMods:appliedenergistics2:rv4-alpha-9"

	provided('org.squiddev:ConfigGen:1.2.5') { exclude group: 'net.minecraftforge' }
}

sourceSets {
	main {
		java {
			exclude 'org/squiddev/plethora/integration/cctweaks/**'
		}
	}
}

processResources {
	// this will ensure that this task is redone when the versions change.
	inputs.property "mod_version", project.mod_version
	inputs.property "mc_version", project.mc_version

	// replace stuff in mcmod.info, nothing else
	from(sourceSets.main.resources.srcDirs) {
		include 'mcmod.info'

		// replace version and mcversion
		expand 'mod_version': mod_version, 'mc_version': mc_version
	}

	// copy everything else, thats not the mcmod.info
	from(sourceSets.main.resources.srcDirs) {
		exclude 'mcmod.info'
	}
}

gradle.projectsEvaluated {
	tasks.withType(JavaCompile) {
		options.compilerArgs << "-Xlint" << "-Xlint:-rawtypes" << "-Xlint:-processing"
	}
}

curseforge {
	apiKey = project.hasProperty('curseForgeApiKey') ? project.curseForgeApiKey : ''

	project {
		id = '248425'
		releaseType = 'release'
		changelog = ''

		relations {
			requiredLibrary 'computercraft'
		}
	}
}
