// For those who want the bleeding edge
buildscript {
	repositories {
		jcenter()
		maven {
			name = "forge"
			url = "http://files.minecraftforge.net/maven"
		}
	}
	dependencies {
		classpath 'net.minecraftforge.gradle:ForgeGradle:2.2-SNAPSHOT'
	}
}

plugins {
	id 'com.matthewprenger.cursegradle' version '1.0.5'
}

buildDir = "build-${mc_version}"

apply plugin: 'net.minecraftforge.gradle.forge'
apply from: 'gradle/scripts/jsonGen.gradle'

version = mc_version + "-" + mod_version
group = "org.squiddev"
archivesBaseName = "plethora"

minecraft {
	version = "${project.mc_version}-${project.forge_version}"
	runDir = "run-${project.mc_version}"
	mappings = "snapshot_20160518"

	replace '${mod_version}', project.mod_version
	replace '${mc_version}', project.mc_version
	replace '${cc_version}', project.cc_version
}

jar {
	manifest {
		attributes(
			'FMLAT': 'Plethora_at.cfg',
		)
	}

	exclude "**/*.java"
}

repositories {
	jcenter()

	// JEI + Tinker's
	maven { url = "http://dvs1.progwml6.com/files/maven" }

	// Baubles
	maven { url = "http://mvn.rx14.co.uk/local/" }

	// ComputerCraft
	ivy { artifactPattern "http://addons-origin.cursecdn.com/files/2311/39/[module][revision](.[ext])" }

	// Chameleon + Storage Drawers
	ivy { artifactPattern "https://addons-origin.cursecdn.com/files/2349/653/[module]-[revision](.[ext])"}
	ivy { artifactPattern "https://addons-origin.cursecdn.com/files/2353/147/[module]-[revision](.[ext])"}

	// CCTweaks + ConfigGen
	maven { url = "https://dl.bintray.com/squiddev/maven" }

	// IC2 API
	maven { url = "http://maven.ic2.player.to/" }

	// Buildcraft
	ivy {
		artifactPattern "http://www.mod-buildcraft.com/releases/BuildCraft/[revision]/[module]-[revision](-[classifier]).[ext]"
	}

	// Tesla
	maven { url 'http://maven.epoxide.xyz' }
}

dependencies {
	compile "ComputerCraft:ComputerCraft:${project.cc_version}"
	deobfCompile "mezz.jei:jei_${project.mc_version}:3.9.7.260"

	provided "org.squiddev:CCTweaks:${project.mc_version}-1.2.3-pr0:dev"
	provided "net.industrial-craft:industrialcraft-2:2.6.99-ex110:dev"
	provided "buildcraft:buildcraft:7.2.7:dev" // Won't run on 1.10.2 but just here for the future
	provided "com.azanor.baubles:Baubles:${project.mc_version}-1.3.3.44:deobf"
	provided "net.darkhax.tesla:Tesla:${mc_version}-1.2.1.50:deobf"

	// Tinker's + Dependencies
	deobfProvided "slimeknights:TConstruct:${project.mc_version}-2.6.1.464"
	deobfProvided "slimeknights.mantle:Mantle:${project.mc_version}-1.1.3.199"

	// Storage drawers + Dependencies
	deobfProvided "curseMods:Chameleon:1.10-2.2.0"
	deobfProvided "curseMods:StorageDrawers:${project.mc_version}-3.5.8"

	provided('org.squiddev:ConfigGen:1.2.5') { exclude group: 'net.minecraftforge' }

	compile "org.squiddev:unborked:${mc_version}-${cc_version}-1.0.2:dev"
}

processResources {
	// this will ensure that this task is redone when the versions change.
	inputs.property "mod_version", project.mod_version
	inputs.property "mc_version", project.mc_version

	// replace stuff in mcmod.info, nothing else
	from(sourceSets.main.resources.srcDirs) {
		include 'mcmod.info'

		// replace version and mcversion
		expand 'mod_version': mod_version, 'mc_version': mc_version
	}

	// copy everything else, thats not the mcmod.info
	from(sourceSets.main.resources.srcDirs) {
		exclude 'mcmod.info'
	}
}

gradle.projectsEvaluated {
	tasks.withType(JavaCompile) {
		options.compilerArgs << "-Xlint" << "-Xlint:-rawtypes"
	}
}

curseforge {
	apiKey = project.hasProperty('curseForgeApiKey') ? project.curseForgeApiKey : ''

	project {
		id = '248425'
		releaseType = 'beta'
		changelog = ''

		relations {
			requiredLibrary 'computercraft'
		}
	}
}
