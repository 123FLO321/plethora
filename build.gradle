// For those who want the bleeding edge
buildscript {
	repositories {
		jcenter()
		maven {
			name = "forge"
			url = "http://files.minecraftforge.net/maven"
		}
	}
	dependencies {
		classpath 'net.minecraftforge.gradle:ForgeGradle:2.2-SNAPSHOT'
	}
}

plugins {
	id 'com.matthewprenger.cursegradle' version '1.0.5'
}

buildDir = "build-${mc_version}"

apply plugin: 'net.minecraftforge.gradle.forge'
apply from: 'gradle/scripts/jsonGen.gradle'

version = mc_version + "-" + mod_version
group = "org.squiddev"
archivesBaseName = "plethora"

minecraft {
	version = "${project.mc_version}-${project.forge_version}-${project.mc_version}"
	runDir = "run-${project.mc_version}"
	mappings = "stable_20"

	replace '${mod_version}', project.mod_version
	replace '${mc_version}', project.mc_version
	replace '${cc_version}', project.cc_version
}

jar {
	manifest {
		attributes(
			'FMLAT': 'Plethora_at.cfg',
		)
	}

	exclude "**/*.java"
}

repositories {
	jcenter()

	// JEI + Tinker's
	maven { url = "http://dvs1.progwml6.com/files/maven" }

	// Baubles
	maven { url = "http://mvn.rx14.co.uk/local/" }

	// ComputerCraft
	ivy { artifactPattern "http://addons-origin.cursecdn.com/files/2291/384/[module][revision](.[ext])" }

	// Chameleon + Storage Drawers
	ivy { artifactPattern "https://addons-origin.cursecdn.com/files/2323/184/[module]-[revision](.[ext])" }
	ivy { artifactPattern "https://addons-origin.cursecdn.com/files/2312/704/[module]-[revision](.[ext])" }

	// CCTweaks + ConfigGen
	maven { url = "https://dl.bintray.com/squiddev/maven" }

	// IC2 API
	maven { url = "http://maven.ic2.player.to/" }

	// Buildcraft
	ivy {
		artifactPattern "http://www.mod-buildcraft.com/releases/BuildCraft/[revision]/[module]-[revision](-[classifier]).[ext]"
	}
}

dependencies {
	compile "ComputerCraft:ComputerCraft:${project.cc_version}"
	deobfCompile "mezz.jei:jei_${project.mc_version}:2.28.9.177"

	provided "org.squiddev:CCTweaks:${project.mc_version}-1.3.2:dev"
	provided "net.industrial-craft:industrialcraft-2:2.3.262-ex18:dev"
	provided "buildcraft:buildcraft:7.2.7:dev"
	provided "com.azanor.baubles:Baubles:${project.mc_version}-1.1.3.0.9:deobf"

	// Tinker's + Dependencies
	deobfProvided "slimeknights:TConstruct:${project.mc_version}-2.2.1.jenkins1005"
	deobfProvided "slimeknights.mantle:Mantle:${project.mc_version}-0.9.2.jenkins102"

	// Storage drawers + Dependencies
	deobfProvided "curseMods:Chameleon:${project.mc_version}-1.1.5"
	deobfProvided "curseMods:StorageDrawers:${project.mc_version}-2.6.6"

	provided('org.squiddev:ConfigGen:1.2.5') { exclude group: 'net.minecraftforge' }
}

processResources {
	// this will ensure that this task is redone when the versions change.
	inputs.property "mod_version", project.mod_version
	inputs.property "mc_version", project.mc_version

	// replace stuff in mcmod.info, nothing else
	from(sourceSets.main.resources.srcDirs) {
		include 'mcmod.info'

		// replace version and mcversion
		expand 'mod_version': mod_version, 'mc_version': mc_version
	}

	// copy everything else, thats not the mcmod.info
	from(sourceSets.main.resources.srcDirs) {
		exclude 'mcmod.info'
	}
}

gradle.projectsEvaluated {
	tasks.withType(JavaCompile) {
		options.compilerArgs << "-Xlint" << "-Xlint:-rawtypes"
	}
}

curseforge {
	apiKey = project.hasProperty('curseForgeApiKey') ? project.curseForgeApiKey : ''

	project {
		id = '248425'
		releaseType = 'release'
		changelog = ''

		relations {
			requiredLibrary 'computercraft'
		}
	}
}
