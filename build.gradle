// For those who want the bleeding edge
buildscript {
	repositories {
		jcenter()
		maven {
			name = "forge"
			url = "http://files.minecraftforge.net/maven"
		}
	}
	dependencies {
		classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
	}
}

plugins {
	id 'com.matthewprenger.cursegradle' version '1.0.9'
}

buildDir = "build-${mc_version}"

apply plugin: 'net.minecraftforge.gradle.forge'

version = mc_version + "-" + mod_version
group = "org.squiddev"
archivesBaseName = "plethora"

minecraft {
	version = "${project.mc_version}-${project.forge_version}"
	runDir = "run-${project.mc_version}"
	mappings = "snapshot_20170708"

	replace '${mod_version}', project.mod_version
	replace '${mc_version}', project.mc_version
	replace '${cc_version}', project.cc_version
}

jar {
	manifest {
		attributes(
			'FMLAT': 'Plethora_at.cfg',
		)
	}

	exclude "**/*.java"
}

repositories {
	jcenter()

	// JEI + Tinker's
	maven { url = "http://dvs1.progwml6.com/files/maven" }

	// ComputerCraft
	maven { url "https://cc.crzd.me/maven/" }

	// Chameleon + Storage Drawers
	ivy { artifactPattern "https://addons-origin.cursecdn.com/files/2446/481/[module]-[revision](.[ext])" }
	ivy { artifactPattern "https://addons-origin.cursecdn.com/files/2447/448/[module]-[revision](.[ext])" }

	// CCTweaks + ConfigGen
	maven { url = "https://dl.bintray.com/squiddev/maven" }

	// IC2 API
	maven { url = "http://maven.ic2.player.to/" }

	// Buildcraft
	ivy {
		artifactPattern "http://www.mod-buildcraft.com/releases/BuildCraft/[revision]/[module]-[revision](-[classifier]).[ext]"
	}

	// Multipart
	maven { url "http://maven.amadornes.com/" }

	// Refined Storage
	maven { url "http://dl.bintray.com/raoulvdberge/dev" }

	// Tesla
	maven { url 'http://maven.epoxide.xyz' }

	// RF
	maven { url = "http://maven.covers1624.net" }

	// Forestry
	ivy { artifactPattern "https://addons-origin.cursecdn.com/files/2445/652/[module]_[revision](.[ext])" }

	// Applied Energistics 2
	ivy { artifactPattern "https://addons-origin.cursecdn.com/files/2356/184/[module]-[revision](.[ext])" }

	// Baubles
	ivy { artifactPattern "https://addons-origin.cursecdn.com/files/2518/667/[module]-[revision](.[ext])"}

	// Blockcraftery
	ivy { artifactPattern "https://addons-origin.cursecdn.com/files/2485/336/[module]-[revision](.[ext])"}

	// CBMP
	maven { url "http://chickenbones.net/maven" }
}

dependencies {
	implementation "dan200.computercraft:ComputerCraft:${project.cc_version}-${project.cc_build}"
	deobfCompile "mezz.jei:jei_${project.mc_version}:4.7.0.68"

	compileOnly "org.squiddev:CCTweaks:1.11.2-1.5.0-pr0:dev" // TODO: Upload initial 1.12 version
	compileOnly "cofh:RedstoneFlux:1.12-2.0.0.1:deobf"
	compileOnly "net.industrial-craft:industrialcraft-2:2.6.99-ex110:dev"
	compileOnly "buildcraft:buildcraft:7.99.14"
	compileOnly "net.darkhax.tesla:Tesla-1.12:1.0.61:deobf"

	// Tinker's + Dependencies
	compileOnly "slimeknights:TConstruct:1.12.2-2.9.0.56"
	compileOnly "slimeknights.mantle:Mantle:1.12-1.3.1.22"

	// Storage drawers + Dependencies
	compileOnly "curseMods:Chameleon:${project.mc_version}-4.1.0"
	compileOnly "curseMods:StorageDrawers:${project.mc_version}-5.2.2"

	compileOnly "MCMultiPart2:MCMultiPart:2.2.2:deobf"
	compileOnly "refinedstorage:refinedstorage:1.5.8-1240"
	compileOnly "curseMods:forestry:${project.mc_version}-5.4.3.128"
	compileOnly "curseMods:appliedenergistics2:rv4-alpha-9" // Won't run on 1.12, but here for the future
	compileOnly "curseMods:Baubles:1.12-1.5.2"
	compileOnly "curseMods:blockcraftery:0.1.3"
	compileOnly("codechicken:ForgeMultipart:1.12-2.3.0.46:deobf") { exclude group: "codechicken" }
	compileOnly "codechicken:CodeChickenLib:1.12-3.1.5.330:deobf"

	compileOnly('org.squiddev:ConfigGen:1.2.5') { exclude group: 'net.minecraftforge' }

	compileOnly "com.google.errorprone:error_prone_annotations:2.0.21"
}

processResources {
	// this will ensure that this task is redone when the versions change.
	inputs.property "mod_version", project.mod_version
	inputs.property "mc_version", project.mc_version

	// replace stuff in mcmod.info, nothing else
	from(sourceSets.main.resources.srcDirs) {
		include 'mcmod.info'

		// replace version and mcversion
		expand 'mod_version': mod_version, 'mc_version': mc_version
	}

	// copy everything else, thats not the mcmod.info
	from(sourceSets.main.resources.srcDirs) {
		exclude 'mcmod.info'
	}
}

gradle.projectsEvaluated {
	tasks.withType(JavaCompile) {
		options.compilerArgs << "-Xlint" << "-Xlint:-rawtypes" << "-Xlint:-processing"
	}
}

curseforge {
	apiKey = project.hasProperty('curseForgeApiKey') ? project.curseForgeApiKey : ''

	project {
		id = '248425'
		releaseType = 'release'
		changelog = ''

		relations {
			requiredLibrary 'computercraft'
		}
	}
}
